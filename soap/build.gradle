repositories {
    mavenCentral()
}

configurations {
    wsdl2Java
    compile.extendsFrom wsdl2Java
}

dependencies{
    wsdl2Java group: 'org.apache.axis2', name: 'axis2-codegen', version: '1.6.2'
    wsdl2Java group: 'org.apache.axis2', name: 'axis2', version: '1.6.2'
    //wsdl2Java group: 'org.apache.xmlbeans', name: 'xmlbeans', version: '2.5.0'    only necessary for efetc_pmc.wsdl
}

//generates the SOAP client code for the NCBI SOAP API
task generateGenBankClientCode {
    //This is a complete list of the wsdl file definitions from NCBI
    def wsdls = [//'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_taxon.wsdl',
            'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/eutils.wsdl',
            'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_seq.wsdl',
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_pubmed.wsdl',
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_pmc.wsdl',       //Use XML Beans, generates roughly 1.2 Million Lines of Java
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_nlmc.wsdl',
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_journals.wsdl',  NO LONGER AVAILABLE
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_omim.wsdl',      NO LONGER AVAILABLE
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_snp.wsdl',
            //'http://eutils.ncbi.nlm.nih.gov/soap/v2.0/efetch_gene.wsdl'
    ]

    wsdls.each { wsdl ->
        doLast {
            javaexec {
                main = 'org.apache.axis2.wsdl.WSDL2Java'
                classpath = configurations.wsdl2Java
                def fs = System.getProperty("file.separator")
                def outputDir = project.projectDir.toString() + fs + 'src' + fs + 'main' + fs + 'java'
                args '--noBuildXML', '-uri', wsdl, '-o', outputDir, '-S', fs
            }
        }
    }
}
compileJava.dependsOn generateGenBankClientCode
